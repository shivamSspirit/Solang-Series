---
title: Mint NFT And Transfer To Another User
author: shivam
moduleNumber: module-5
modulePart: module-5-b
thisFileName: mint-and-transfer
orderNumber: '3'
modulePartDescription: In this part of the module, you will implement a Solidity Solang program for NFT minting and transfer (NFT mint authority is PDA).
---

**Now we want to mint token into user wallet for this**

We need to create an external function that looks like this:

- **`external`** functions can exclusively be called by other contracts or directly by an RPC. They are also present in the contract’s ABI or IDL.

If the function has a single return value, this can be assigned to a variable. If the function has multiple return values, these can be assigned using the [Destructuring Statement](https://solang.readthedocs.io/en/v0.3.3/language/statements.html#destructuring) assignment statement.

It is also possible to call functions on other contracts, which is known as calling **external** functions. The called function must be declared public. Calling **external** functions requires ABI encoding the arguments and ABI decoding the return values. This is much more costly than an internal function call.

```solidity
    @mutableSigner(payer)
    @mutableAccount(tokenAccount)
    @account(owner)
    @mutableAccount(mint)
    @mutableAccount(pdaAccount)
    function mintTo() external {
        // Create an associated token account for the owner to receive the minted token
        SplToken.create_associated_token_account(
            tx.accounts.payer.key, // payer account
            tx.accounts.tokenAccount.key, // associated token account address
            tx.accounts.mint.key, // mint account
            tx.accounts.owner.key // owner account
        );

        // Mint 1 token to the associated token account
        _mintTo(
            tx.accounts.mint.key, // mint account
            tx.accounts.tokenAccount.key, // token account
            1, // amount
            tx.accounts.pdaAccount.key
        );

        // // Remove mint authority from mint account
        _removeMintAuthority(
            tx.accounts.mint.key, // mint
            tx.accounts.pdaAccount.key
        );
    }
```

let’s break this down

by using these accounts

```solidity

    @mutableSigner(payer)
    @mutableAccount(tokenAccount)
    @account(owner)
    @mutableAccount(mint)
    @mutableAccount(pdaAccount)
```

## Privileges of annotations:

- **`@mutableSigner(name)`**: Signer for this transaction (the signer of this transaction).
- **`@mutableAccount(name)`**: An account that will mutate (change its state) in this transaction.
- **`@account(name)`**: General account used in this transaction.


## Write MintTo instructions

Create a **`mintTo`** **external** function with no params.

In the function body:

We create an Associated Token Account (ATA), which we learned about in previous lectures.

You already know why we need ATA, but here we are creating an ATA for the NFT owner who mints the NFT. For this, we are making a CPI to the SPL library and using the **`SplToken.create_associated_token_account`** method to create an ATA account for the owner.

For this, we need to provide these four accounts.

```solidity
  // Create an associated token account for the owner to receive the minted token
        SplToken.create_associated_token_account(
            tx.accounts.payer.key, // payer account
            tx.accounts.tokenAccount.key, // associated token account address
            tx.accounts.mint.key, // mint account
            tx.accounts.owner.key // owner account
        );
```

Here, these four accounts are obtained from **`tx.accounts`**. This is how we can access accounts that we provide in annotations.
           

```solidity
tx.accounts.payer.key, // payer account (who pay for transaction)
             tx.accounts.tokenAccount.key, // associated token account address
            tx.accounts.mint.key, // mint account
            tx.accounts.owner.key // owner account(owner of this ata)
```

next we move to mint token into user account 

```solidity
// Mint 1 token to the associated token account
        _mintTo(
            tx.accounts.mint.key, // mint account
            tx.accounts.tokenAccount.key, // token account
            1, // amount
            tx.accounts.pdaAccount.key
        );
```


## Re-implement MintTO

For this, we need to reimplement the **`mintTo`** method again because we are signing with PDA.

The re-implemented method looks like this:

In this **`_mintTo`** method, we get some parameters. Then, we are invoking the SPL token program to mint tokens to the user's token account using PDA as the mint authority.

```solidity
// Invoke the token program to mint tokens to a token account, using a PDA as the mint authority
    function _mintTo(address mint, address account, uint64 amount, address pdaAccount) private {
        // Independently derive the PDA address from the seeds, bump, and programId
        (address pda, bytes1 _bump) = try_find_program_address(["mint_authority"], type(pda_mint_authority).program_id);
        require(pdaAccount == pda, 'INVALID_PDA');

        // Prepare instruction data
        bytes instructionData = new bytes(9);
        instructionData[0] = uint8(7); // MintTo instruction index
        instructionData.writeUint64LE(amount, 1); // Amount to mint

        // Prepare accounts required by instruction
        AccountMeta[3] metas = [
            AccountMeta({pubkey: mint, is_writable: true, is_signer: false}),
            AccountMeta({pubkey: account, is_writable: true, is_signer: false}),
            AccountMeta({pubkey: pda, is_writable: true, is_signer: true}) // mint authority
        ];

        // Invoke the token program with prepared accounts and instruction data
        SplToken.tokenProgramId.call{accounts: metas, seeds: [["mint_authority", abi.encode(_bump)]]}(instructionData);
    }
```

Steps breakdown:

First, we derive the PDA using the same seed. Then, we check the validity of the PDA.

Now, we prepare the instruction data to send.

Next, we manually write account metas.

Then, we invoke a cross-program invocation to the SPLToken program to mint NFT to the user's token account.

## Remove Mint Auth

Next, we remove the mint authority from the mint account.

For this, we need the same re-implemented methods to remove the mint authority (because we are signing with PDA).

```solidity
// Remove mint authority from mint account
_removeMintAuthority(
    tx.accounts.mint.key, // mint
    tx.accounts.pdaAccount.key
);

```

## Re-implement Remove

The `_removeMintAuthority` method looks like this:

This is a cross-program invocation to the SPLToken program to remove the mint authority.

We are doing the same steps like deriving PDA, preparing account meta, building instruction data, and then invoking the SPLToken program.

```solidity
function _removeMintAuthority(address mintAccount, address pdaAccount) private {
        // Independently derive the PDA address from the seeds, bump, and programId
        (address pda, bytes1 _bump) = try_find_program_address(["mint_authority"], type(pda_mint_authority).program_id);
        require(pdaAccount == pda, 'INVALID_PDA');

		AccountMeta[2] metas = [
			AccountMeta({pubkey: mintAccount, is_signer: false, is_writable: true}),
			AccountMeta({pubkey: pda, is_signer: true, is_writable: false}) // mint authority
		];

		bytes instructionData = new bytes(9);
		instructionData[0] = uint8(6); // SetAuthority instruction index
		instructionData[1] = uint8(0); // AuthorityType::MintTokens
		instructionData[3] = 0;

        // Invoke the token program with prepared accounts and instruction data
        SplToken.tokenProgramId.call{accounts: metas, seeds: [["mint_authority", abi.encode(_bump)]]}(instructionData);
	}
```

That's it for creating a mint account and metadata account for the minted token. Then, mint the token into the minter's wallet and remove the mint authority.

now we are moving to 

## Spl Token Transfer

Next, we write a method for transferring the minted NFT to another account.

For this, we need to create an external function that will contain a SPLToken.transfer CPI.

This library call method contains three accounts:

- From token account
- To token account
- Owner of NFT (signer)

And one more argument:

- The token amount (in our case, one).

Write this below **`_removeMintAuthority`**.

```solidity
// transfer NFT TO another user
@mutableAccount(from) // token account to transfer from
@mutableAccount(to) // token account to transfer to
@signer(owner)
function transferNft() external {

   SplToken.transfer(
            tx.accounts.from.key, // from associated token account address
            tx.accounts.to.key, // to associated token account address
            tx.accounts.owner.key, // owner account
            1
        );
    }
```

That's it. This is all about the Solidity program for NFT minting and transferring, where the mint authority is set as PDA.

Now we are moving to set up for testing.